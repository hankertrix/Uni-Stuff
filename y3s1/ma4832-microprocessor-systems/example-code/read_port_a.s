; Reads PortA Bit 4-7 (Pins PA4 - PA7 are connected to dip switch)

; GPIO_PORTA address

GPIO_PORTA_DATA_R		EQU 0x400043FC
GPIO_PORTA_DIR_R		EQU 0x40004400
GPIO_PORTA_AFSEL_R		EQU 0x40004420
GPIO_PORTA_PUR_R		EQU 0x40004510
GPIO_PORTA_DEN_R		EQU 0x4000451C
GPIO_PORTA_AMSEL_R		EQU 0x40004528
GPIO_PORTA_PCTL_R		EQU 0x4000452C
PA_4567					EQU 0x400043C0	; PortA bit 4-7

SYSCTL_RCGCGPIO_R		EQU 0x400FE608	; GPIO run mode clock gating control

		THUMB
		AREA	DATA, ALIGN=4
		EXPORT	Result [DATA,SIZE=4]
Result	SPACE	4

		AREA	|.text|, CODE, READONLY, ALIGN=2
		THUMB
		EXPORT	Start

Start

; initialize Port A
; enable digital I/O, ensure alt. functions off.

; activate clock for PortA
		LDR R1, =SYSCTL_RCGCGPIO_R 		; R1 = address of SYSCTL_RCGCGPIO_R
		LDR R0, [R1]					;
		ORR R0, R0, #0x01				; turn on GPIOA clock
		STR R0, [R1]
		NOP								; allow time for clock to finish
		NOP
		NOP

; no need to unlock Port A bits

; disable analog mode
		LDR R1, =GPIO_PORTA_AMSEL_R
		LDR R0, [R1]
		BIC R0, R0, #0xF0				; disable analog mode on PortA bit 4-7
		STR R0, [R1]

;configure as GPIO
		LDR R1, =GPIO_PORTA_PCTL_R
		LDR R0, [R1]
		BIC R0, R0,#0x00FF0000			; clear PortA bit 4 & 5
		BIC R0, R0,#0xFF000000			; clear PortA bit 6 & 7
		STR R0, [R1]

;set direction register
		LDR R1, =GPIO_PORTA_DIR_R
		LDR R0, [R1]
		BIC R0, R0, #0xF0				; set PortA bit 4-7 input (0: input, 1: output)
		STR R0, [R1]

; disable alternate function
		LDR R1, =GPIO_PORTA_AFSEL_R
		LDR R0, [R1]
		BIC R0, R0, #0xF0				; disable alternate function on PortA bit 4-7
		STR R0, [R1]

; pull-up resistors on switch pins
		LDR R1, =GPIO_PORTA_PUR_R		;
		LDR R0, [R1]					;
		ORR R0, R0, #0xF0				; enable pull-up on PortA bit 4-7
		STR R0, [R1]

; enable digital port
		LDR R1, =GPIO_PORTA_DEN_R
		LDR R0, [R1]
		ORR R0, R0, #0xF0				; enable digital I/O on PortA bit 4-7
		STR R0, [R1]

		LDR R1, =PA_4567

Loop
		LDR R0, [R1]					; R0 = dip switch status
		LDR R2, =Result
		STR R0,[R2]						; store data

		B Loop

		ALIGN							; make sure the end of this section is aligned
		END								; end of file
